import("//build/toolchain/emscripten/emscripten.gni")

template("typescript_target") {
    npx_path = "$emscripten_node_path/npx"
    if (host_os == "win") {
        npx_path += ".cmd"
    }

    action(target_name) {
        target_dir = get_label_info(target_name, "dir") 
        forward_variables_from(invoker, ["sources", "typescript_output_directory", "deps"])
        script = "//build/toolchain/emscripten/typescript_compile.py"
        args = [ rebase_path(target_dir), "$npx_path", "webpack" ]
        outputs = []
        foreach(source_file, sources) {
            output_file = string_replace(source_file, ".ts", ".js")
            outputs += [ "$typescript_output_directory/$output_file" ]
        }
    }
}
template("emscripten_target")
{
    #For now only these targets are supported
    assert(is_emscripten && defined(invoker.emscripten_target_type) 
    && (invoker.emscripten_target_type == "executable" || invoker.emscripten_target_type == "shared_library" || invoker.emscripten_target_type == "static_library"))

    base_target_name = target_name

    typescript_all_sources = []
    if (defined(invoker.typescript_sources)) {
        typescript_all_sources += invoker.typescript_sources
    }
    if (defined(invoker.typescript_merge_sources)) {
        typescript_all_sources += invoker.typescript_merge_sources
    }
    if(typescript_all_sources != []) {
        typescript_target("typescript_compile_$base_target_name") {
            typescript_output_directory = root_out_dir
            sources = typescript_all_sources
        }
    }

    target(invoker.emscripten_target_type, base_target_name) {
        forward_variables_from(invoker, "*")

        if (!defined(defines)) {
            defines = []
        }
        if (!defined(deps)) {
            deps = []
        }
        if(!defined(inputs)) {
            inputs = []
        }
        inputs += typescript_all_sources
        if(!defined(ldflags)) {
            ldflags = []
        }

        if (defined(static_deps))
        {
            foreach(static_dep, static_deps) {
                deps += [ static_dep ]
                static_dep_target_name = get_label_info(static_dep, "name")
                defines += [ string_to_upper(static_dep_target_name) + "_STATIC" ]
            }
        }

        if(typescript_all_sources != []) {
            deps += [ ":typescript_compile_$base_target_name" ]
        }
        if (defined(typescript_merge_sources)) {
            foreach(merge_source, typescript_merge_sources) {
                output_file = rebase_path(merge_source, "", root_out_dir)
                ldflags += ["--js-library", string_replace(output_file, ".ts", ".js")]           
            }
        }
        
        # emar doesn't support --embind-emit-tsd
        if(emscripten_target_type == "static_library") {
            defines += [ string_to_upper(base_target_name) + "_STATIC" ]
        } 
    }  
}